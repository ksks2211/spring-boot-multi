rootProject.name = 'multi'


["common", "comp", "web","server"].each {

    def compDir = new File(rootDir, it)
    if(!compDir.exists()){
        compDir.mkdirs()
    }

    compDir.eachDir {subDir ->

        def gradleFile = new File(subDir.absolutePath, "build.gradle")
        if(!gradleFile.exists()){
            gradleFile.text =
                    """

                    dependencies {

                    }

                    """.stripIndent(20)
        }

        [
                "src/main/java/kr/yoonyeong",
                "src/main/resources",
                "src/test/java/kr/yoonyeong",
                "src/test/resources"
        ].each {srcDir->
            def srcFolder = new File(subDir.absolutePath, srcDir)
            if(!srcFolder.exists()){
                srcFolder.mkdirs()
            }
        }

        String projectName = ":${it}-${subDir.name}"
        include projectName
        project(projectName).projectDir = subDir
    }
}

